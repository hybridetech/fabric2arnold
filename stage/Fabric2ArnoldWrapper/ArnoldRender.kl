require FileIO;

/** Setup Arnold to render to the provided callback */
function Boolean setArnoldRenderToDisplay(Ref<AtDisplayCallback> callback ) 
{
  AtNode driver = AiNodeLookUpByName("display_onscreen");
  if (!driver.isValid()) {
    // create an output driver - this passes rendered pixels to our callback
    driver = AiNode("driver_display");
    AiNodeSetStr(driver, "name", "display_onscreen");
  }

  // create a gaussian filter node
  AtNode gfilter = AiNodeLookUpByName("myfilter");
  if (!gfilter.isValid())
  {
    gfilter = AiNode("gaussian_filter");
    AiNodeSetStr(gfilter, "name", "myfilter");      
  }
  SetAtDisplayCallback(driver, callback);

  // assign the driver and the filter to the outputs
  AtNode options = AiUniverseGetOptions();
  AtArray outputs_array = AiArrayAllocate(1, 1, AI_TYPE_STRING);
  AiArraySetStr(outputs_array, 0, "RGBA RGBA myfilter display_onscreen");
  AiNodeSetArray(options, "outputs", outputs_array);

  return true;
}

/** Setup Arnold to render to a jpg file with path outFileName */
function Boolean setArnoldRenderToFile(String outFilename) 
{
  // If, at some point we supported Async rendering, this call would be necessery
  if (AiRendering())
  {
    AiRenderInterrupt();
  }

  // create a gaussian filter node
  AtNode gfilter = AiNodeLookUpByName("myfilter");
  if (!gfilter.isValid())
  {
    gfilter = AiNode("gaussian_filter");
    AiNodeSetStr(gfilter, "name", "myfilter");      
  }

  // For now, assume we are writing to a JPG file
  // This could be fixed in future.
  // Reset file output
  AtNode driver = AiNodeLookUpByName("jpg_driver");
  if (!driver.isValid()) {
    // create an output driver - this pushes renders to disk
    driver = AiNode("driver_jpeg");
    AiNodeSetStr(driver, "name", "jpg_driver");
  }

  // Set filename we are writing out to
  AiNodeSetStr(driver, "filename", outFilename);

  // assign the driver and the filter to the outputs
  AtNode options = AiUniverseGetOptions();
  AtArray outputs_array = AiArrayAllocate(1, 1, AI_TYPE_STRING);
  AiArraySetStr(outputs_array, 0, "RGBA RGBA myfilter jpg_driver");
  AiNodeSetArray(options, "outputs", outputs_array);

  return true;
}

/** Write current Arnold scene cache to disk */
function SInt32 doArnoldRenderToASS(String outFilename) {
  FilePath fp(outFilename);
  // By default, we set the ASS file to write a jpg
  String jpgFilename = fp.replaceExtension('jpg').string();
  setArnoldRenderToFile(jpgFilename);

  return AiASSWrite(outFilename, AI_NODE_ALL, false, true);
}
